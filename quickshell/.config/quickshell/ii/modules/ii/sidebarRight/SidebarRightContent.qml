import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import qs.modules.common.functions
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Quickshell
import Quickshell.Bluetooth
import Quickshell.Hyprland

import qs.modules.ii.sidebarRight.quickToggles
import qs.modules.ii.sidebarRight.quickToggles.classicStyle

import qs.modules.ii.sidebarRight.bluetoothDevices
import qs.modules.ii.sidebarRight.nightLight
import qs.modules.ii.sidebarRight.volumeMixer
import qs.modules.ii.sidebarRight.wifiNetworks
Item {
    id: root
    property alias contentBackground: sidebarRightBackground
    property int sidebarWidth: Appearance.sizes.sidebarWidth
    property int sidebarPadding: 10
    property string settingsQmlPath: Quickshell.shellPath("settings.qml")
    property bool showAudioOutputDialog: false
    property bool showAudioInputDialog: false
    property bool showBluetoothDialog: false
    property bool showNightLightDialog: false
    property bool showWifiDialog: false
    property bool showQuickCommandDialog: false
    property bool editMode: false

    width: parent?.width ?? implicitWidth
    height: parent?.height ?? implicitHeight

    opacity: 0
    transform: Translate {
        id: rootTranslate
        x: 24
        Behavior on x {
            animation: Appearance.animation.elementMoveEnter.numberAnimation.createObject(this)
        }
    }
    scale: 0.98
    Behavior on opacity {
        animation: Appearance.animation.elementMoveEnter.numberAnimation.createObject(this)
    }
    Behavior on scale {
        animation: Appearance.animation.elementMoveEnter.numberAnimation.createObject(this)
    }

    Connections {
        target: GlobalStates
        function onSidebarRightOpenChanged() {
            if (GlobalStates.sidebarRightOpen) {
                root.opacity = 1;
                rootTranslate.x = 0;
                root.scale = 1;
            } else {
                root.opacity = 0;
                rootTranslate.x = 24;
                root.scale = 0.98;
            }
        }
    }

    Component.onCompleted: {
        if (GlobalStates.sidebarRightOpen) {
            root.opacity = 1;
            rootTranslate.x = 0;
            root.scale = 1;
        }
    }

    Connections {
        target: GlobalStates
        function onSidebarRightOpenChanged() {
            if (!GlobalStates.sidebarRightOpen) {
                root.showWifiDialog = false;
                root.showBluetoothDialog = false;
                root.showAudioOutputDialog = false;
                root.showAudioInputDialog = false;
            }
        }
    }

    implicitHeight: sidebarRightBackground.implicitHeight
    implicitWidth: sidebarRightBackground.implicitWidth

    StyledRectangularShadow {
        target: sidebarRightBackground
    }
    Rectangle {
        id: sidebarRightBackground

        anchors.fill: parent
        implicitHeight: parent.height - Appearance.sizes.hyprlandGapsOut * 2
        implicitWidth: sidebarWidth - Appearance.sizes.hyprlandGapsOut * 2
        color: ColorUtils.applyAlpha(Appearance.colors.colLayer0Base, 0.72)
        border.width: 1
        border.color: Appearance.colors.colLayer0Border
        radius: Appearance.rounding.screenRounding - Appearance.sizes.hyprlandGapsOut + 1
        clip: true

        ColumnLayout {
            anchors.fill: parent
            anchors.margins: sidebarPadding
            spacing: sidebarPadding

            SystemButtonRow {
                Layout.fillHeight: false
                Layout.fillWidth: true
                Layout.topMargin: 5
                Layout.bottomMargin: 0
            }

            SecondaryTabBar {
                id: sidebarTabBar
                Layout.fillWidth: true
                SecondaryTabButton {
                    buttonText: Translation.tr("Status")
                    buttonIcon: "monitor_heart"
                }
                SecondaryTabButton {
                    buttonText: Translation.tr("Notifications")
                    buttonIcon: "notifications"
                }
                SecondaryTabButton {
                    buttonText: Translation.tr("Planner")
                    buttonIcon: "calendar_month"
                }
            }

            StackLayout {
                id: sidebarStack
                Layout.fillWidth: true
                Layout.fillHeight: true
                currentIndex: sidebarTabBar.currentIndex
                clip: true

                Item { // Status page
                    id: statusPage
                    Layout.fillWidth: true
                    Layout.fillHeight: true

                    Flickable {
                        id: statusFlickable
                        anchors.fill: parent
                        clip: true
                        contentHeight: statusColumn.implicitHeight
                        ScrollBar.vertical: StyledScrollBar {}

                        ColumnLayout {
                            id: statusColumn
                            width: statusFlickable.width
                            spacing: sidebarPadding

                            Loader {
                                id: slidersLoader
                                Layout.fillWidth: true
                                visible: active
                                active: {
                                    const configQuickSliders = Config.options.sidebar.quickSliders
                                    if (!configQuickSliders.enable) return false
                                    if (!configQuickSliders.showMic && !configQuickSliders.showVolume && !configQuickSliders.showBrightness) return false;
                                    return true;
                                }
                                sourceComponent: QuickSliders {}
                            }

                            LoaderedQuickPanelImplementation {
                                styleName: "classic"
                                sourceComponent: ClassicQuickPanel {}
                            }

                            LoaderedQuickPanelImplementation {
                                styleName: "android"
                                sourceComponent: AndroidQuickPanel {
                                    editMode: root.editMode
                                }
                            }

                            SystemStatusGraph {
                                Layout.fillWidth: true
                            }

                            PowerStatus {
                                Layout.fillWidth: true
                            }

                            NowPlaying {
                                Layout.fillWidth: true
                            }
                        }
                    }
                }

                CenterWidgetGroup {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                }

                BottomWidgetGroup {
                    fillHeight: true
                    Layout.fillWidth: true
                }
            }
        }
    }

    ToggleDialog {
        shownPropertyString: "showAudioOutputDialog"
        dialog: VolumeDialog {
            isSink: true
        }
    }

    ToggleDialog {
        shownPropertyString: "showAudioInputDialog"
        dialog: VolumeDialog {
            isSink: false
        }
    }

    ToggleDialog {
        shownPropertyString: "showBluetoothDialog"
        dialog: BluetoothDialog {}
        onShownChanged: {
            if (!shown) {
                Bluetooth.defaultAdapter.discovering = false;
            } else {
                Bluetooth.defaultAdapter.enabled = true;
                Bluetooth.defaultAdapter.discovering = true;
            }
        }
    }

    ToggleDialog {
        shownPropertyString: "showNightLightDialog"
        dialog: NightLightDialog {}
    }

    ToggleDialog {
        shownPropertyString: "showWifiDialog"
        dialog: WifiDialog {}
        onShownChanged: {
            if (!shown) return;
            Network.enableWifi();
            Network.rescanWifi();
        }
    }

    component ToggleDialog: Loader {
        id: toggleDialogLoader
        required property string shownPropertyString
        property alias dialog: toggleDialogLoader.sourceComponent
        readonly property bool shown: root[shownPropertyString]
        anchors.fill: parent

        onShownChanged: if (shown) toggleDialogLoader.active = true;
        active: shown
        onActiveChanged: {
            if (active) {
                item.show = true;
                item.forceActiveFocus();
            }
        }
        Connections {
            target: toggleDialogLoader.item
            function onDismiss() {
                toggleDialogLoader.item.show = false
                root[toggleDialogLoader.shownPropertyString] = false;
            }
            function onVisibleChanged() {
                if (!toggleDialogLoader.item.visible && !root[toggleDialogLoader.shownPropertyString]) toggleDialogLoader.active = false;
            }
        }
    }

    component LoaderedQuickPanelImplementation: Loader {
        id: quickPanelImplLoader
        required property string styleName
        Layout.alignment: item?.Layout.alignment ?? Qt.AlignHCenter
        Layout.fillWidth: item?.Layout.fillWidth ?? false
        visible: active
        active: Config.options.sidebar.quickToggles.style === styleName
        Connections {
            target: quickPanelImplLoader.item
            function onOpenAudioOutputDialog() {
                root.showAudioOutputDialog = true;
            }
            function onOpenAudioInputDialog() {
                root.showAudioInputDialog = true;
            }
            function onOpenBluetoothDialog() {
                root.showBluetoothDialog = true;
            }
            function onOpenNightLightDialog() {
                root.showNightLightDialog = true;
            }
            function onOpenWifiDialog() {
                root.showWifiDialog = true;
            }
        }
    }

    component SystemButtonRow: Item {
        implicitHeight: Math.max(uptimeContainer.implicitHeight, systemButtonsRow.implicitHeight)

        Rectangle {
            id: uptimeContainer
            anchors {
                top: parent.top
                bottom: parent.bottom
                left: parent.left
            }
            color: ColorUtils.applyAlpha(Appearance.colors.colLayer1Base, 0.38)
            radius: height / 2
            border.width: 1
            border.color: Appearance.colors.colLayer0Border
            clip: true
            implicitWidth: uptimeRow.implicitWidth + 24
            implicitHeight: uptimeRow.implicitHeight + 8
            
            Row {
                id: uptimeRow
                anchors.centerIn: parent
                spacing: 8
                CustomIcon {
                    id: distroIcon
                    anchors.verticalCenter: parent.verticalCenter
                    width: 25
                    height: 25
                    source: SystemInfo.distroIcon
                    colorize: true
                    color: Appearance.colors.colOnLayer0
                }
                StyledText {
                    anchors.verticalCenter: parent.verticalCenter
                    font.pixelSize: Appearance.font.pixelSize.normal
                    color: Appearance.colors.colOnLayer0
                    text: Translation.tr("Up %1").arg(DateTime.uptime)
                    textFormat: Text.MarkdownText
                }
            }
        }

        ButtonGroup {
            id: systemButtonsRow
            anchors {
                top: parent.top
                bottom: parent.bottom
                right: parent.right
            }
            color: Appearance.colors.colLayer1
            padding: 4

            QuickToggleButton {
                toggled: root.editMode
                visible: Config.options.sidebar.quickToggles.style === "android"
                buttonIcon: "edit"
                onClicked: root.editMode = !root.editMode
                StyledToolTip {
                    text: Translation.tr("Edit quick toggles") + (root.editMode ? Translation.tr("\nLMB to enable/disable\nRMB to toggle size\nScroll to swap position") : "")
                }
            }
            QuickToggleButton {
                toggled: false
                buttonIcon: "restart_alt"
                onClicked: {
                    Quickshell.execDetached(["hyprctl", "reload"])
                    Quickshell.reload(true);
                }
                StyledToolTip {
                    text: Translation.tr("Reload Hyprland & Quickshell")
                }
            }
            QuickToggleButton {
                toggled: false
                buttonIcon: "settings"
                onClicked: {
                    GlobalStates.sidebarRightOpen = false;
                    Quickshell.execDetached(["qs", "-p", root.settingsQmlPath]);
                }
                StyledToolTip {
                    text: Translation.tr("Settings")
                }
            }
            QuickToggleButton {
                toggled: false
                buttonIcon: "terminal"
                onClicked: {
                    root.showQuickCommandDialog = true;
                }
                StyledToolTip {
                    text: Translation.tr("Quick command")
                }
            }
            QuickToggleButton {
                toggled: false
                buttonIcon: "power_settings_new"
                onClicked: {
                    GlobalStates.sessionOpen = true;
                }
                StyledToolTip {
                    text: Translation.tr("Session")
                }
            }
        }
    }

    ToggleDialog {
        shownPropertyString: "showQuickCommandDialog"
        dialog: Component {
            QuickCommandDialog {}
        }
    }

    component QuickCommandDialog: WindowDialog {
        id: qcRoot
        property var qcHistory: []
        property int qcHistoryIndex: -1
        backgroundHeight: 200

        onVisibleChanged: {
            if (visible) qcInput.forceActiveFocus()
        }

        Keys.onPressed: (event) => {
            if (event.key === Qt.Key_Escape) {
                qcRoot.dismiss()
            } else if (event.key === Qt.Key_Up) {
                if (qcHistoryIndex > 0) {
                    qcHistoryIndex--
                    qcInput.text = qcHistory[qcHistoryIndex]
                }
                event.accepted = true
            } else if (event.key === Qt.Key_Down) {
                if (qcHistoryIndex < qcHistory.length - 1) {
                    qcHistoryIndex++
                    qcInput.text = qcHistory[qcHistoryIndex]
                } else {
                    qcHistoryIndex = qcHistory.length
                    qcInput.text = ""
                }
                event.accepted = true
            }
        }

        WindowDialogTitle {
            text: Translation.tr("Quick Command")
        }

        WindowDialogSeparator {
            Layout.topMargin: -22
            Layout.leftMargin: 0
            Layout.rightMargin: 0
        }

        StyledTextInput {
            id: qcInput
            Layout.fillWidth: true
            Layout.leftMargin: 10
            Layout.rightMargin: 10
            Layout.topMargin: 5
            font.pixelSize: Appearance.font.pixelSize.normal
            color: Appearance.colors.colOnSurface

            Keys.onPressed: (event) => {
                if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                    runCmd()
                    event.accepted = true
                }
            }
        }

        WindowDialogButtonRow {
            DialogButton {
                buttonText: Translation.tr("Run")
                onClicked: runCmd()
            }

            Item {
                Layout.fillWidth: true
            }

            DialogButton {
                buttonText: Translation.tr("Cancel")
                onClicked: qcRoot.dismiss()
            }
        }

        function runCmd() {
            var cmd = qcInput.text.trim()
            if (cmd.length === 0) return

            qcHistory.push(cmd)
            qcHistoryIndex = qcHistory.length
            qcInput.text = ""

            Quickshell.execDetached(["bash", "-c", cmd])
            GlobalStates.sidebarRightOpen = false
        }
    }
}
